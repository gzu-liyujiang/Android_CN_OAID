/*
 * Copyright (c) 2013-present, 贵州纳雍穿青人李裕江 <1032694760@qq.com>, All Right Reserved.
 */

configurations {
    all {
        //此处可用于解决依赖冲突问题，参阅https://developer.android.google.cn/studio/build/dependencies#duplicate_classes
        resolutionStrategy {
            force androidxLibrary.core
            force androidxLibrary.lifecycle
            force androidxLibrary.fragment
            force androidxLibrary.recyclerview
            force androidxLibrary.documentfile
        }
    }
}

android {

    compileSdkVersion rootProject.ext.compileSdkVersion as int
    buildToolsVersion rootProject.ext.buildToolsVersion
    // 配置资源名前缀提示，避免资源名重复
    //resourcePrefix project.name == 'app' ? '' : project.name.toLowerCase() + '_'

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion as int
        targetSdkVersion rootProject.ext.targetSdkVersion as int
        versionCode gitGitCommitNumber() as int
        versionName getGitLatestTag()
        println("versionCode: $versionCode\nversionName: $versionName")
        buildConfigField "String", "BUILD_TIME", '"' + new Date().format("yyyy-MM-dd HH:mm") + '"'
        buildConfigField "String", "GIT_BRANCH", '"' + getGitBranch() + '"'
        buildConfigField "String", "GIT_LATEST_COMMIT_HASH", '"' + getGitLatestCommitHashShort() + '"'
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            res.srcDirs = ['src/main/res']
            java.srcDirs = ['src/main/java']
            assets.srcDirs = ['src/main/assets']
            aidl.srcDirs = ['src/main/aidl']
            jni.srcDirs = ['cpp', 'jni', 'src/main/cpp', 'src/main/jni']
            jniLibs.srcDirs = ['libs', 'src/main/jniLibs']
        }
    }

    aaptOptions {
        cruncherEnabled false
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'InvalidPackage'
    }

    compileOptions {
        encoding 'UTF-8'
        // jdk 1.8 开始支持 lambda 表达式
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL*'
        exclude 'META-INF/ASL*'
        exclude 'META-INF/proguard/*'
        exclude 'META-INF/*.version'
        exclude 'androidsupportmultidexversion.txt'
        exclude 'LICENSE*'
        exclude 'junit/*/*'
        exclude 'publicsuffixes.gz'
        exclude 'okhttp3/internal/publicsuffix/publicsuffixes.gz'
        //exclude 'assets/libwbsafeedit_x86'
        //exclude 'assets/libwbsafeedit_x86_64'
    }

}
